~ "aoc" ~ Day

$ ............
$ ........0...
$ .....0......
$ .......0....
$ ....0.......
$ ......A.....
$ ............
$ ............
$ ........A...
$ .........A..
$ ............
$ ............
Sample ←
Input  ← Day 8

Parse  ← ⍜♭⊛-@. ⊜∘⊸≠@\n
ByFreq ← ⊞= +1⇡/↥⊸♭
# ? Positions Ranges
FreqOffsets ← ≡/⊂ ⊞× ≡⍜↻↘₁°⊏ ⊞-.
FreqNodes   ← ♭₂+⊃≡¤FreqOffsets⊚
Solve ← (
  ⊃(/↥△|◴/◇⊂⍚FreqNodes⊙¤ByFreq) # Find nodes
  ⧻⊚ /×/≠⊞< ⊂0 ⊙⍉               # Remove OoB
)
Part₁ ← Solve⊙[2] Parse
Part₂ ← ⊙⋅◌ ⍥(◡(Solve⊙⇡) ⋅⊙+₁₀)∞ 0⊙2 Parse
⊟⊃Part₁Part₂ Input
